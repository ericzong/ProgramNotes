# 变量

## 两种赋值与获取变量值的方法

AutoHotkey 中，赋值两种方法：**传统方法**（赋值号为等号 `=`） 和 **表达式方法**（赋值号为冒号-等号 `:=`）。

相应地，在不同方法的赋值语句中，应使用相应的获取变量值的方法。

|            | 传统方法 | 表达式方法 |
| ---------- | -------- | ---------- |
| 赋值       | =        | :=         |
| 获取变量值 | %Var%    | Var        |
| 字符串     | string   | "string"   |

如果使用了错误的赋值，可能会得到意料之外的结果，并且跟赋值给变量什么有关。下面我们一一讨论。

**将数值赋值给变量**

这种情况下，无论用哪种赋值方法都是可以的，效果上几乎是等效的（除非有变量名与所赋值的数值一样，见后文相关讨论）。

```
Var = 42	; 42
Var := 42	; 42
```

**将字符串赋值给变量**

使用传统赋值方法时，字符串不用双引号引起来；但使用表达式赋值方法时，字符串必须用双引号引起来。

```
Var = This is a statement		; This is a statement (正确)
Var := "This is a statement"	; This is a statement （正确）
Var = "This is a statement"		; "This is a statement" （错误）
Var := This is a statement		; 空 （错误）
```

使用传统赋值方法时，赋值号右边所有字符都被视为字符串的一部分，包括双引号。所以，行 3 结果是包括两端的双引号的。

使用表达式赋值方法时，赋值号右边未用双引号引起来的部分都将解析为变量。因此，行 4 被解析为 4 个变量（This、is、a、statement）的拼接，而它们均为空，所以拼接结果亦为空。

**将变量赋值给变量**

这种情况下，赋值方法应与获取变量值的方法对应，才能得到预期的结果。

```
t = test
Var = %t%	; test （正确）
Var := t	; test （正确）
Var = t		; t （错误）
Var := %t%	; 空 （错误）
```

行 4 是传统赋值，因此，右边的 t 相应地被解析为字符串 "t"，而不是变量 t。

行 5 是表达式赋值，因此，右边没有用双引号引起来的部分都被视为变量，故获取到变量 %t% 的值——空。

可见，混合使用两种赋值是很容易出错的，除非你能牢牢记住这些规则不搞混。

因此，最佳实践就是永远只使用一种赋值。

首选应是表达式语法，因为，这种语法跟其他许多语言几乎一致。

```
MyNumber := 42
MyString := "This is a string"
CopyOfVar := Var
```

不过，所有命令（特殊注明除外）默认使用传统模式，比如：

```
MsgBox The value in the variable named Var is %Var%.
```

如果想一致地使用表达式语法，那么可以使用百分号和空格把参数从传统模式改变为表达式模式：

```
MsgBox % "The value in the variable named Var is " . Var . "."  ; 使用句点连接两个字符串，亦可省略
```

当然，从上例也能看出来，传统模式下字符串的拼接更为自然，接近一些语言中的模板字符串概念。

## 纯数字变量

变量名可以完全由数字组成，并不能用在表达式中，而需使用传统方法引用。但通常为了可读性，不会人为这样命名。

全数字变量名的一个常见之处是，命令行参数。

需要注意的是，非表达式 if 语句（概念见下文）中，左侧总是变量名。这将导致左侧的全数字字面值总是引用到命令行参数或前脚本中定义的全数字变量，但这很可能不是脚本的本意。如：

```
if 0 < 3 ; 非表达式 if 语句，左侧总是变量名
{
    MsgBox At least 3 parameters required.
}
```

当命令行参数少于 3 个时，会弹出对话框。

## 空字符串

注意空字符串在各种情况下的表现形式（是否需要双引号）。

|            | 示例          |
| ---------- | ------------- |
| 传统赋值   | Var =         |
| 表达式赋值 | Var := ""     |
| 传统 if    | if Var =      |
| 表达式 if  | if(Var <> "") |

# 表达式

## if 语句

if 语句分为表达式 if 语句和 传统 if 语句，区别在于是否有括号。

表达式 if 语句，表达式用圆括号括起来（不必整个表达式括起来，多个逻辑表达式可各自单独括起来），例外：if 后第一项为函数调用或类似 not 或 ! 运算符时，可省略括号。

传统 if 语句，没有圆括号，且变量用一对百分号引起来。